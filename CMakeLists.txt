########################################################################
# CMakeLists.txt
#
# Author: Matthias Moller
# Copyright (C) 2021-2023 by the IgANet authors
#
# This file is part of the IgANet project
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
########################################################################

# Set a default coefficient numeric types if not specified
if(NOT IGANET_PERFTESTS_COEFF_TYPE)
  set (IGANET_PERFTESTS_COEFF_TYPE "double" CACHE STRING
    "Coefficient type(float, double)" FORCE)
endif()
set_property(CACHE IGANET_PERFTESTS_COEFF_TYPE PROPERTY STRINGS
  "float" "double")

########################################################################
# Config file
########################################################################
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/perftest_config.hpp.in
  "${CMAKE_CURRENT_BINARY_DIR}/perftest_config.hpp" @ONLY)

########################################################################
# Include directories
########################################################################
include_directories(${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_BINARY_DIR})

########################################################################
# Define remote repositoies
########################################################################
include(FetchContent)

########################################################################
# GoogleTest
########################################################################
FetchContent_Declare(
  googletest  
  URL https://github.com/google/googletest/archive/refs/heads/main.zip
  )
# For Windows: Prevent overriding the parent project's compiler/linker settings
set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)

########################################################################
# BSplineLib
########################################################################
FetchContent_Declare(
  bsplinelib
  URL https://github.com/tataratat/BSplineLib/archive/refs/heads/tataratat.zip
  FIND_PACKAGE_ARGS
  )
set(BSPLINELIB_BUILD_PUGIXML OFF CACHE BOOL "" FORCE)
set(pugixml_DIR ${pugixml_BINARY_DIR})
FetchContent_MakeAvailable(googletest bsplinelib)
FetchContent_GetProperties(bsplinelib)
include_directories(${bsplinelib_SOURCE_DIR}/Sources)

########################################################################
# Collect perftest sources
########################################################################
file(GLOB PERFTESTS *.cxx)

foreach(perftest ${PERFTESTS})
  file(RELATIVE_PATH filename ${CMAKE_CURRENT_SOURCE_DIR} ${perftest})
  if (CMAKE_CUDA_COMPILER)
    set_source_files_properties(${filename} PROPERTIES LANGUAGE CUDA)
  endif()
  
  string(REPLACE ".cxx" "" name ${filename})
  add_executable(${name} ${filename})

  if(NOT ${CMAKE_VERSION} VERSION_LESS "3.16.0")
    target_precompile_headers(${name} REUSE_FROM iganet_pch)
  endif()

  # LibTorch
  target_link_libraries(${name} PUBLIC "${TORCH_LIBRARIES}")

  # BSplineLib
  target_link_libraries(${name} PUBLIC pugixml)
  target_link_libraries(${name} PUBLIC splines)

  # Google test
  target_link_libraries(${name} PUBLIC gtest_main)
  
  if (IGANET_WITH_MATPLOT)
    target_link_libraries(${name} PUBLIC Matplot++::matplot)
  endif()
  target_compile_definitions(${name} PUBLIC PERFTEST_DISABLE_SHORT_MACROS)
  add_test(${name} ${name})
endforeach()
